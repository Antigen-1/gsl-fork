; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

[Setup]
AppName=GNU Scientific Library
AppVerName=GNU Scientific Library @VERSION@
;AppPublisher=Brian Gough
;AppPublisherURL=http://www.network-theory.co.uk/gsl/
;AppSupportURL=http://www.network-theory.co.uk/gsl/
;AppUpdatesURL=http://www.network-theory.co.uk/gsl/
DefaultDirName={pf}\GSL
DefaultGroupName=GNU Scientific Library
OutputBaseFilename=gsl-@VERSION@-setup
UsePreviousAppDir=no
UsePreviousGroup=no
AlwaysCreateUninstallIcon=yes
LicenseFile="..\Copying"
UninstallFilesDir={app}\uninstall
;CompressLevel=9
;InfoBeforeFile=Readme
;InfoAfterFile=msvc\README.txt
; uncomment the following line if you want your installation to run on NT 3.51 too.
; MinVersion=4,3.51

[Files]
Source: "README.txt"; DestDir: "{app}"; DestName: "README.txt";  Flags: isreadme;
Source: "..\Copying"; DestDir: "{app}"; DestName: "Copying.txt";
Source: "..\NEWS"; DestDir: "{app}"; DestName: "NEWS.txt";
Source: "..\THANKS"; DestDir: "{app}"; DestName: "THANKS.txt";
Source: "..\AUTHORS"; DestDir: "{app}"; DestName: "AUTHORS.txt";
;
Source: "..\*.dsp"; DestDir: "{app}\src"; Flags: recursesubdirs;
Source: "..\*.dsw"; DestDir: "{app}\src"; Flags: recursesubdirs;
Source: "..\*.h"; DestDir: "{app}\src"; Flags: recursesubdirs;
Source: "..\*.c"; DestDir: "{app}\src"; Flags: recursesubdirs;
;
Source: "..\gsl\*.*"; DestDir: "{app}\include\gsl";
;
Source: "Release\libgsl.lib"; DestDir: "{app}\lib";
Source: "Release\libgslcblas.lib"; DestDir: "{app}\lib"; 
Source: "Debug\libgsl.lib"; DestDir: "{app}\lib";  DestName: "libgsld.lib"
Source: "Debug\libgslcblas.lib"; DestDir: "{app}\lib"; DestName: "libgslcblasd.lib";

Source: "..\doc\html\*.chm"; DestDir: "{app}\doc"; 

Source: "demo\*.*"; DestDir: "{userdesktop}\GSL @VERSION@ demo";

[Icons]
Name: "{group}\GNU Scientific Library Reference Manual"; Filename: "{app}\doc\gsl-ref.chm"
Name: "{userdesktop}\GNU Scientific Library Reference Manual @VERSION@"; Filename: "{app}\doc\gsl-ref.chm"
Name: "{userdesktop}\GNU Scientific Library @VERSION@"; Filename: "{app}"; WorkingDir: "{app}";
Name: "{group}\README"; Filename: "{app}\README.txt"
Name: "{group}\NEWS"; Filename: "{app}\NEWS.txt"
Name: "{group}\LICENSE"; Filename: "{app}\COPYING.txt"

[Messages]
SetupLdrStartupMessage=This will install the %1. Do you wish to continue?
WelcomeLabel2=This will install the [name/ver] on your computer.%n%nIt is strongly recommended that you close all other applications you have running before continuing. This will help prevent any conflicts during the installation process.
LicenseLabel2=Do you accept all the terms of the preceding License Agreement? If you choose No, Setup will close. To install the [name], you must accept this agreement.
SelectDirDesc=Where should the [name] be installed?
SelectDirLabel=Select the folder where you would like the [name] to be installed, then click Next.
ReadyLabel1=Setup is now ready to begin installing the [name] on your computer.
InstallingLabel=Please wait while Setup installs the [name] on your computer.
FinishedLabelNoIcons=Setup has finished installing the [name] on your computer.
FinishedLabel=Setup has finished installing the [name] on your computer. The application may be launched by selecting the installed icons.
ConfirmUninstall=Are you sure you want to completely remove the %1 and all of its components?
UninstallStatusLabel=Please wait while the %1 is removed from your computer.
UninstalledAll=The %1 was successfully removed from your computer.

[Registry]
Root: HKCU; Subkey: "Software\Microsoft\DevStudio\6.0\Build System\Components\Platforms\Win32 (x86)\Directories"; ValueType: string; ValueName: "Include Dirs"; ValueData: "{olddata};{app}\include"; Flags: dontcreatekey
Root: HKCU; Subkey: "Software\Microsoft\DevStudio\6.0\Build System\Components\Platforms\Win32 (x86)\Directories"; ValueType: string; ValueName: "Library Dirs"; ValueData: "{olddata};{app}\lib"; Flags: dontcreatekey
Root: HKCU; Subkey: "Software\Microsoft\DevStudio\6.0\Build System\Components\Platforms\Win32 (x86)\Directories"; ValueType: string; ValueName: "Source Dirs"; ValueData: "{olddata};{app}\src;{app}\include\gsl"; Flags: dontcreatekey
